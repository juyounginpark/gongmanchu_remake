label chapter6_1:
    scene dorm_room_night with fade
    "(지잉- 지잉-)"
    "(지잉-)"

    main "우으음…"
    main "이 신성한 주말 아침에…"
    main "누구지…?"

    "화면에 알림 3개가 떠 있다."

    main "!!"
    main "이게 다 뭐야?"
    main "왜 시간대도 다 제각각이냐고…"
    main "음... 누구 연락을 볼까?"

    # 알림 화면에서 클릭으로 선택
    call screen phone_notifications_select

    $ selected_partner = _return

   
    if selected_partner == "pjy":
        # 주연 선배 선택
        $ likeJuyoun += 20
        main "(뭐해…?)"
        main "(뭐 했다고 해야 하지? 방금 일어났다고 하면 게으르다 생각할 텐데…)"

        python:
            pjy_conv = PhoneConversation(
                [["나", None], ["박주연", "pjy standard"]],
                [
                    0,
                    ("아침 먹고 산책 중입니다ㅎㅎ", "오전 8:33"),
                    1,
                    ("오 부지런하다!", "오전 8:35"),
                    ("(대충 멋지다는 이모티콘)", "오전 8:35"),
                    ("아 맞다", "오전 8:36"),
                    ("내일 축제하는 거 알아?", "오전 8:36"),
                    0,
                    ("네!", "오전 8:36"),
                    1,
                    ("나 축제 가고 싶은데ㅠㅠ", "오전 8:36"),
                    ("같이 갈 사람이 없어서", "오전 8:36"),
                    ("혹시 같이 돌아다녀 줄 수 있어?", "오전 8:36"),
                    ("(ㅠㅠ 이모티콘)", "오전 8:37"),
                    0,
                    ("네 좋아요!", "오전 8:38"),
                    1,
                    ("오 고마워~", "오전 8:38"),
                    ("그럼 축제 때 보자! 연락해!", "오전 8:38"),
                    0,
                    ("네!", "오전 8:38"),
                ]
            )
        call screen phone(pjy_conv)

        main "(우와…! 이건 데이트 신청?)"
        main "(설마… 나… 가능성이 있을지도?)"
        main "우와… 내가 주연 선배랑?"
        main "벌써 심장이 두근두근대…"
        main "뭘 해야 하지? 빨리 옷 사야겠다…!"
        main "… 현서한테 물어봐야지."
        $ date_partner = "pjy"

    elif selected_partner == "jsk":
        # 성경 선택
        $ likeJsk += 20
        main "(새벽 두 시에 안녕이라니…)"
        main "(그래도 답장은 해야겠지?)"

        python:
            jsk_conv = PhoneConversation(
                [["나", None], ["정성경", "jsk standard"]],
                [
                    0,
                    ("(안녕 이모티콘)", "오전 8:33"),
                    "새벽에 잠이 잘 안 왔나 봐? ㅎㅎ",
                    1,
                    ("응", "오전 8:33"),
                    "책 읽다가 잠들었어.",
                    0,
                    ("오, 책 좋아하나 봐?", "오전 8:34"),
                    1,
                    ("응", "오전 8:34"),
                    "있잖아",
                    0,
                    ("응?", "오전 8:34"),
                    1,
                    ("이번 축제 때, 문학 체험관이 열린다고 하더라고. 책을 좋아하는 사람들이 직접 꾸민 부스도 있어. 또 조용한 분위기라서, 사람 많아도 편하게 둘러볼 수 있을 것 같아. 혹시 시간 되면, 같이 가줄 수 있을까? 편하게 말해줘. 그냥 같이 보면 좋을 것 같아서.", "오전 8:36"),
                    0,
                    ("그래 그러자!", "오전 8:36"),
                ]
            )
        call screen phone(jsk_conv)

        main "(엄청난 장문이네…)"
        main "(뭐… 괜찮겠지?)"

        pause 2.0
        main "(…왜 답장이 안 오지?)"

        python:
            jsk_conv2 = PhoneConversation(
                [["나", None], ["정성경", "jsk standard"]],
                [
                    1,
                    ("응 고마워.", "오전 8:38"),
                    ("그때 연락할게.", "오전 8:38"),
                    0,
                    ("응 축제 때 봐!", "오전 8:38"),
                ]
            )
        call screen phone(jsk_conv2)

        main "볼수록 특이한 친구 같네."
        main "문학에는 완전 젬병인데… 미리 책 좀 읽어둬야겠다."
        $ date_partner = "jsk"

    elif selected_partner == "pyr":
        # 여름 선택
        $ likeYeoreum += 20
        main "(11시…? 어제 피곤해서 바로 잤더니…)"

        python:
            pyr_conv = PhoneConversation(
                [["나", None], ["박여름", "pyr standard"]],
                [
                    0,
                    ("미안! 바로 잠들어버렸어ㅎㅎ", "오전 8:33"),
                    1,
                    ("아냐! 괜찮아 ㅎㅎ", "오전 8:33"),
                    ("그게,", "오전 8:34"),
                    ("내일 축제한다는데 같이 가지 않을래?", "오전 8:34"),
                    0,
                    ("좋아!", "오전 8:35"),
                    1,
                    ("(좋다는 이모티콘)", "오전 8:36"),
                    ("알겠어!", "오전 8:36"),
                    ("내일 연락할게!", "오전 8:36"),
                    0,
                    ("응 내일 봐!", "오전 8:37"),
                ]
            )
        call screen phone(pyr_conv)

        main "(응…?)"
        main "(정말… 얘 뭐지…)"
        $ date_partner = "pyr"

    jump chapter6_2
